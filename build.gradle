plugins {
  id("java")
  id("maven-publish")

  alias(libraries.plugins.champeau.jmh)
}

group = "me.kvdpxne"
version = "0.1.0"

def ENABLE_PREVIEW = "--enable-preview"
def targetJavaVersion = 21
java {
  def javaVersion = JavaVersion.toVersion(targetJavaVersion)

  sourceCompatibility = javaVersion
  targetCompatibility = javaVersion

  if (JavaVersion.current() < javaVersion) {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
  }
}

dependencies {
  jmh(libraries.bundles.jmh)
  testImplementation(libraries.jupiter)
}

jmh {
  jvmArgs.add(ENABLE_PREVIEW)
}

jmhRunBytecodeGenerator {
  jvmArgs.add(ENABLE_PREVIEW)
}

tasks.withType(JavaCompile).configureEach {
  if (10 <= targetJavaVersion || JavaVersion.current().isJava10Compatible()) {
    options.release.set(targetJavaVersion)
  }

  options.compilerArgs += [
    // Enable preview features for the Java compiler.
    ENABLE_PREVIEW
  ]
}

tasks.withType(JavaExec).configureEach {
  jvmArgs.add(ENABLE_PREVIEW)
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()
  jvmArgs += ENABLE_PREVIEW
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId = "me.kvdpxne"
      artifactId = "forty2"
      version = "0.1.0"

      from components.java
    }
  }
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}